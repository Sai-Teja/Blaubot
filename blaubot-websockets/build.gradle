apply plugin: 'java'

// next two lines are needed for publish to bintray (jcenter repository), see https://github.com/danielemaddaluno/gradle-jcenter-publish
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

// This is the version used when deploying the artifact to jcenter
version = rootProject.blaubot_version
sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile project(':blaubot')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Apache 2.0
    compile 'io.netty:netty-all:4.0.26.Final'
}

jar {
    from ("${projectDir}") {
        include('LICENSE.blaubot-websockets.txt')
        include('README.blaubot-websockets.md')
    }
}


// now maven adjustments
def siteUrl = 'http://blaubot.hgross.eu'            // Homepage URL of the library
def gitUrl = 'https://github.com/Blaubot/Blaubot'   // Git repository URL
group = "eu.hgross"                                 // Maven Group
def libraryDescription = 'Websocket Adapter for Blaubot.'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                artifactId 'blaubot-websockets'
                inceptionYear '2015'
                packaging 'jar'

                name 'Blaubot WebSocket adapter'
                url siteUrl
                description = libraryDescription

                // Set your license
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'hgross'
                        name 'Henning Gross'
                        email 'mail.to@henning-gross.de'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    from ("${projectDir}") {
        include('LICENSE.blaubot-websockets.txt')
        include('README.blaubot-jsr82.md')
    }
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        // it is the name that appears in bintray when logged
        name = "blaubot-websockets"
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = 'https://github.com/Blaubot/Blaubot/issues'
        licenses = ["MIT"]
        dryRun = rootProject.dryRun //Whether to run this as dry-run, without deploying
        publish = rootProject.autoPublish //If version should be auto published after an upload
        labels = ['blaubot']
        version {
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password") //Optional. The passphrase for GPG signing'
            }
//            mavenCentralSync {
//                sync = true //Optional (true by default). Determines whether to sync the version to Maven Central.
//                user = properties.getProperty("bintray.oss.user") //OSS user token
//                password = properties.getProperty("bintray.oss.password") //OSS user password
//                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
//            }
        }
    }
}